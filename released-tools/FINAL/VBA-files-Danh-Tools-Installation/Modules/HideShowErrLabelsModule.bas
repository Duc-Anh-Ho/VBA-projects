Attribute VB_Name = "HideShowErrLabelsModule"
' AUTO HIDE AND SHOW CHART ERROR LABEL
' Author: DANH
' Version: 2.0.0
' Update: 2022/10/10
' Check README.md for more information

Option Explicit

Private Const VERSION = "v2.0.0"
Private Const AUTHOR_PROMPT As String = "AUTHOR: "
Private Const AUTHOR_NAME = "DANH"
Private fileSystem As Object
Private userResponse As VbMsgBoxResult
Private app As Application
Private wb As Workbook
Private ws As Worksheet
Private chartTarget As Chart
Private excelErrors() As Variant

Private Static Function initializeVariables() As Boolean
    'ThisWorkbook.Author = AUTHOR_NAME
    Set fileSystem = CreateObject("Scripting.FileSystemObject")
    Set app = Application
    If app.ActiveWorkbook Is Nothing Or app.ActiveSheet Is Nothing Then
        MsgBox _
            Prompt:=AUTHOR_PROMPT & _
                "Can not access file, please re-open!!", _
            Buttons:=vbOKOnly + vbExclamation, _
            Title:=AUTHOR_NAME
        initializeVariables = False
        Exit Function
    Else
        Set wb = app.ActiveWorkbook
        Set ws = wb.ActiveSheet
    End If
    Let excelErrors = Array("#DIV?0!", "#N/A", "#NAME?", "#NULL!", "#NUM!", "#REF!", "#VALUE!") 'Common error excel
    If ActiveChart Is Nothing Then
        MsgBox _
            Prompt:=AUTHOR_PROMPT & _
                "Please select a chart first!", _
            Buttons:=vbOKOnly + vbExclamation, _
            Title:=AUTHOR_NAME
            initializeVariables = False
        Exit Function
    Else
        Set chartTarget = ActiveChart
    End If
    initializeVariables = True
End Function

Private Static Sub hideErrorChartLabels(Optional ByVal isHide As Boolean = True)
    Dim seriesItem As Object
    Dim labelItem As Object
    Dim item As Variant
     'Loop through trend series
    For Each seriesItem In chartTarget.FullSeriesCollection
        seriesItem.HasDataLabels = True
        'Loop through each point of DataLabels (not through DataLabels)
        For Each labelItem In seriesItem.Points
            If isHide Then
                'Loop through errors code
                For Each item In excelErrors
                    If labelItem.datalabel.Text = item Then
                        labelItem.datalabel.Text = "" ' Cheating null String
                        'NOTE: Can't use ShowValue or Delete cause error -2147024809
                    End If
                Next item
            ElseIf Not isHide Then
                If labelItem.datalabel.Text = "" Then
                        labelItem.datalabel.Delete
                        labelItem.datalabel.ShowValue = True
                End If
            End If
        Next labelItem
    Next seriesItem
End Sub


'MAIN
Public Static Sub hide(Optional ByVal isHide As Boolean = True)
On Error GoTo ErrorHandle
    If Not initializeVariables Then GoTo ExecuteProcedure
    Call hideErrorChartLabels(isHide)
'    Call hideErrorChartLabels(isHide)
GoTo ExecuteProcedure
ErrorHandle:
    Call tackleErrors
ExecuteProcedure:
End Sub

'Errors
Private Static Sub tackleErrors()
    Select Case Err.Number
        Case 0
        'Can't enable Addin
        Case 1004
        'VBA file have password
        Case 50289
            MsgBox _
                Prompt:=Err.Description & _
                    vbNewLine & _
                    AUTHOR_PROMPT & _
                    "Can not access this VBA file because of been protected by a password!", _
                Buttons:=vbCritical + vbOKOnly, _
                Title:=AUTHOR_NAME
        'Un-handled Error
        Case Else
            Call errorDisplay
    End Select
    Application.ScreenUpdating = True
    On Error GoTo 0
End Sub

Private Static Sub errorDisplay()
    Dim errorMessage As String: Let errorMessage = _
        "Error # " & Str(Err.Number) & _
        " was generated by " & Err.Source & _
        Chr(13) & "Error Line: " & Erl & _
        Chr(13) & Err.Description
    MsgBox _
        Prompt:=errorMessage, _
        Title:="Error", _
        HelpFile:=Err.HelpFile, _
        Context:=Err.HelpContext
End Sub
